module Test_Formatter_mod
   use funit
   use iso_fortran_env
   use PFL_Formatter
   use PFL_SeverityLevels
   use PFL_LogRecord
   use MockDateFormat_mod
   use gftl2_UnlimitedVector
   use gftl2_StringUnlimitedMap
      
   implicit none

   public :: Test_Formatter

@suite(name='Test_Formatter_suite')

@TestCase
   type, extends(TestCase) :: Test_Formatter
      type (LogRecord) :: rec
   contains
      procedure :: setUp   
      procedure :: tearDown 
   end type Test_Formatter

   type (UnlimitedVector), target :: empty

contains

   
   subroutine setUp(this)
      class (Test_Formatter), intent(inout) :: this

      mockdf%Y = 2015
      mockdf%M = 1
      mockdf%D = 2
      mockdf%HH = 3
      mockdf%MM = 4
      mockdf%SS = 5
      mockdf%MS = 123

   end subroutine setUp

   
   subroutine tearDown(this)
      class (Test_Formatter), intent(inout) :: this
   end subroutine tearDown


   @Test
   subroutine test_formatDefault(this)
      class (Test_Formatter), intent(inout) :: this
      type (Formatter) :: f

      call initLogRecord(this%rec, 'A', INFO, 'Goodbye')
      f = Formatter('%(message)')
      @assertEqual('Goodbye', f%format(this%rec))
      
   end subroutine test_formatDefault
   

@Test
   subroutine test_formatint32(this)
      class (Test_Formatter), intent(inout) :: this

      type (UnlimitedVector), target :: args
      type (Formatter) :: f
      
      args = UnlimitedVector()
      call args%push_back(1)
      call initLogRecord(this%rec, 'A', INFO, 'Goodbye %i1.1', args=args)

      f = Formatter('Hello %(message)a')
      @assertEqual('Hello Goodbye 1', f%format(this%rec))
      
   end subroutine test_formatint32
   

@test
   subroutine test_datefmtMessage_default(this)
      class (Test_Formatter), intent(inout) :: this
      type (StringUnlimitedMap) :: extra
      character(len=32) :: date
      type (Formatter) :: f

      ! Create a LogRecord
      this%rec%name = 'someRec'
      this%rec%level = INFO
      this%rec%message_format = 'datefmt'
      this%rec%args => empty

      ! record has name, level and message + opt args and/or extra
      ! Format the specified record as text: format(record)
      call mockdf%fillDateAndTime(this%rec)
      f = Formatter('%(asctime)a')
      date = '2015-01-02 03:04:05.123'
      @assertEqual(date, f%format(this%rec))

   end subroutine test_datefmtMessage_default

@test
   subroutine test_datefmtMessage(this)
      class (Test_Formatter), intent(inout) :: this
      type (StringUnlimitedMap) :: extra
      character(len=20) :: date
      type (Formatter) :: f

      ! Create a LogRecord
      this%rec%name = 'someRec'
      this%rec%level = INFO
      this%rec%message_format = 'datefmt'
      this%rec%args => empty

      ! record has name, level and message + opt args and/or extra
      ! Format the specified record as text: format(record)
      call mockdf%fillDateAndTime(this%rec)
      f = Formatter(fmt='%(asctime)', datefmt='%(Y)i4.4~-%(M)i2.2~-%(D)i2.2')
      write(date,'(i4.4,a,i2.2,a,i2.2)') mockdf%Y,'-',mockdf%M,'-',mockdf%D
      @assertEqual(date, f%format(this%rec))

   end subroutine test_datefmtMessage


@test
   subroutine test_datefmt(this)
      class (Test_Formatter), intent(inout) :: this
      type (StringUnlimitedMap) :: extra
      character(len=20) :: date
      type (Formatter) :: f

      ! Create a LogRecord
      this%rec%name = 'someRec'
      this%rec%level = INFO
      this%rec%message_format = 'datefmt'
      this%rec%args => empty

      ! record has name, level and message + opt args and/or extra
      ! Format the specified record as text: format(record)
      call mockdf%fillDateAndTime(this%rec)
      f = Formatter(fmt='%(asctime)a', &
                         datefmt='%(HH)i2.2~-%(MM)i2.2~-%(SS)i2.2')
      write(date,'(i2.2,a,i2.2,a,i2.2)') mockdf%HH,'-',mockdf%MM,'-',mockdf%SS
      @assertEqual(date, f%format(this%rec))

   end subroutine test_datefmt


@Test
   subroutine test_formatWithName(this)
      class (Test_Formatter), intent(inout) :: this
      type (UnlimitedVector), target :: args
      type (Formatter) :: f

      args = UnlimitedVector()
      call args%push_back(1)

      call initLogRecord(this%rec, 'A', INFO, 'Goodbye %i1.1', args=args)
      f = Formatter('%(name)a %(message)a')
      @assertEqual('A Goodbye 1', f%format(this%rec))
      
   end subroutine test_formatWithName
   

@test
   subroutine test_format_with_extra(this)
      class (Test_Formatter), intent(inout) :: this      
      character(len=:), allocatable :: logMessage
      type (StringUnlimitedMap) :: dictionary
      type (Formatter) :: f

      call dictionary%insert('one',1)
      call dictionary%insert('two',2)
      f = Formatter('Hello %(one)i1.1 %(two)i1.1', extra=dictionary)

      logMessage = f%format(LogRecord('name', INFO, 'Hello'))
      @assertEqual('Hello 1 2', logMessage)
   end subroutine test_format_with_extra
   
@test
   subroutine test_format_with_short_name(this)
      class (Test_Formatter), intent(inout) :: this      
      character(len=:), allocatable :: logMessage
      type (StringUnlimitedMap) :: dictionary
      type (Formatter) :: f

      call initLogRecord(this%rec, 'A.B.C.D', INFO, 'Goodbye')

      f = Formatter('%(short_name)')
      logMessage = f%format(this%rec)
      @assertEqual('D', logMessage)
   end subroutine test_format_with_short_name
   
@test
   subroutine test_format_with_line_number(this)
      class (Test_Formatter), intent(inout) :: this      
      character(len=:), allocatable :: logMessage
      type (StringUnlimitedMap) :: dictionary
      type (Formatter) :: f

      call initLogRecord(this%rec, 'name', INFO, 'message', line=15)

      f = Formatter('Line: %(line)i5')
      logMessage = f%format(this%rec)
      @assertEqual('Line:    15', logMessage)
   end subroutine test_format_with_line_number
   

@test
   subroutine test_format_with_filename(this)
      class (Test_Formatter), intent(inout) :: this      
      character(len=:), allocatable :: logMessage
      type (StringUnlimitedMap) :: dictionary
      type (Formatter) :: f

      call initLogRecord(this%rec, 'name', INFO, 'message', file='foo.F90')

      f = Formatter('File: %(file)a20')
      logMessage = f%format(this%rec)
      @assertEqual('File:              foo.F90', logMessage)
   end subroutine test_format_with_filename
   
@test
   subroutine test_format_with_basename(this)
      class (Test_Formatter), intent(inout) :: this      
      character(len=:), allocatable :: logMessage
      type (StringUnlimitedMap) :: dictionary
      type (Formatter) :: f

      call initLogRecord(this%rec, 'name', INFO, 'message', file='bar/cat/foo.F90')

      f = Formatter('File: %(basename)a20')
      logMessage = f%format(this%rec)
      @assertEqual('File:              foo.F90', logMessage)
   end subroutine test_format_with_basename
   

end module Test_Formatter_mod
