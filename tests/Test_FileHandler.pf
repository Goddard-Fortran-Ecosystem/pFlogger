module Test_FileHandler_mod
   use ASTG_SeverityLevels_mod
   use ASTG_FileHandler_mod
   use ASTG_LogRecord_mod
   use pfunit_mod
   implicit none

@TestCase
   type, extends(TestCase) :: Test_FileHandler
      type (FileHandler) :: handler
   contains
      procedure :: setUp   
      procedure :: tearDown 
   end type Test_FileHandler

contains

   
   subroutine setUp(this)
      class (Test_FileHandler), intent(inout) :: this
      this%handler = FileHandler('logFile')
   end subroutine setUp

   
   subroutine tearDown(this)
      class (Test_FileHandler), intent(inout) :: this

      integer :: unit

      call this%handler%close()

      ! delete file
      open(newunit=unit, file='logFile', status='old')
      close(unit, status='delete')


   end subroutine tearDown

   
@Test
   subroutine test_emitBasic(this)
      class (Test_FileHandler), intent(inout) :: this   

      integer :: unit
      type (LogRecord) :: record
      character(len=32) :: foundMessage

      record = LogRecord('name', INFO, 'hello')
      call this%handler%handle(record)

      unit = this%handler%getUnit()
      rewind(unit)
      read(unit, '(a)') foundMessage

      @assertEqual('INFO: '//record%getMessage(), foundMessage)

   end subroutine test_emitBasic

   
@Test
   subroutine test_emitBelowThreshold(this)
      use iso_fortran_env, only: IOSTAT_END
      class (Test_FileHandler), intent(inout) :: this

      type (FileHandler) :: handler
      type (LogRecord) :: record
      character(len=32) :: foundMessage
      integer :: iostatus
      
      record = LogRecord('name', DEBUG, 'hello')
      call handler%setLevel(DEBUG)
      call this%handler%handle(record)

      rewind(this%handler%getUnit())
      read(this%handler%getUnit(),'(a)', IOSTAT=iostatus) foundMessage      

      @assertEqual(IOSTAT_END, iostatus)

   end subroutine test_emitBelowThreshold

   
@Test
   subroutine test_emitAboveThreshold(this)
      class (Test_FileHandler), intent(inout) :: this

      type (FileHandler) :: handler
      type (LogRecord) :: record
      character(len=32) :: foundMessage
      integer :: iostatus
      
      record = LogRecord('name', ERROR, 'hello')
      call handler%setLevel(ERROR)
      call this%handler%handle(record)

      rewind(this%handler%getUnit())
      read(this%handler%getUnit(),'(a)', IOSTAT=iostatus) foundMessage      
      @assertEqual('ERROR: '//record%getMessage(), foundMessage)

   end subroutine test_emitAboveThreshold


   ! Often it is desirable for the log file not to be created until/if
   ! a message is sent.  The optional logical argument "delay" on the
   ! contstructor is used to indicate that configuration.
   @test
   subroutine test_delay(this)
      class (Test_FileHandler), intent(inout) :: this

      type (LogRecord) :: record
      type (FileHandler) :: handler
      character(len=*), parameter :: FILE_NAME = 'delayLog'
      logical :: exist
      integer :: unit

      handler = FileHandler(FILE_NAME, delay=.true.)
      inquire(file=FILE_NAME, exist=exist)
      @assertFalse(exist)

      record = LogRecord('me', INFO, 'hello')
      call handler%handle(record)
      inquire(file=FILE_NAME, exist=exist)
      @assertTrue(exist)
      
      call handler%close()
      open(newunit=unit, file=FILE_NAME, status='old')
      close(unit, status='delete')

   end subroutine test_delay

end module Test_FileHandler_mod

