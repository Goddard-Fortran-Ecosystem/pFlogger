module Test_RotatingFileHandler_mod
   use iso_fortran_env
   use ASTG_SeverityLevels_mod
   use ASTG_RotatingFileHandler_mod
   use pfunit_mod
   implicit none

@TestCase
   type, extends(TestCase) :: Test_RotatingFileHandler
      type (RotatingFileHandler) :: rotHandler0
      type (RotatingFileHandler) :: rotHandler
   contains
      procedure :: setUp   
      procedure :: tearDown 
   end type Test_RotatingFileHandler

contains

   
   subroutine setUp(this)
      class (Test_RotatingFileHandler), intent(inout) :: this
      this%rotHandler0 = RotatingFileHandler('logFile0')
      this%rotHandler  = RotatingFileHandler('logFile',  &
           maxBytes=60_int64, backupCount=5)
   end subroutine setUp

   
   subroutine tearDown(this)
      class (Test_RotatingFileHandler), intent(inout) :: this

      integer :: unit

      call this%rotHandler0%close()
      call this%rotHandler%close()

      ! delete file
      open(newunit=unit, file='logfile', status='old')
      close(unit, status='delete')
      open(newunit=unit, file='logfile0', status='old')
      close(unit, status='delete')

   end subroutine tearDown

   
@Test
   subroutine test_emitBasic(this)
      ! Make sure RotatingFileHandler defaults to FileHandler
      class (Test_RotatingFileHandler), intent(inout) :: this   

      integer :: unit
      character(len=:), allocatable :: message
      character(len=32) :: foundMessage

      message = 'hello'
      call this%rotHandler0%emit(INFO, message)
      
      unit = this%rotHandler0%getUnit()
      rewind(unit)
      read(unit, '(a)') foundMessage

      @assertEqual('INFO: '//message, foundMessage)

   end subroutine test_emitBasic

   
@Test
   subroutine test_shouldRollover(this)
      class (Test_RotatingFileHandler), intent(inout) :: this   

      integer :: unit
      character(len=:), allocatable :: message
      integer :: i
      
      message = 'hello'
      do i=1,6
      ! rotHandler should rollOver after 6 records = 72 bytes
         call this%rotHandler%emit(INFO, message)
      end do
      
      @assertTrue(this%rotHandler%shouldRollover())

   end subroutine test_shouldRollover

   
end module Test_RotatingFileHandler_mod

