module Test_LoggerManager_mod
   use pfunit_mod
   use PFL_LoggerManager_mod 
   use PFL_AbstractLogger_mod
   use PFL_Logger_mod
   use PFL_RootLogger_mod
   use PFL_SeverityLevels_mod
   implicit none

contains

   
@test
   subroutine test_getLogger()
      type (LoggerManager) :: manager
      class (Logger), pointer :: loggerA
      class (Logger), pointer :: loggerB
      class (Logger), pointer :: loggerC

      manager = LoggerManager(RootLogger(WARNING))
      loggerA => manager%getLogger('A')
      loggerB => manager%getLogger('B')
      ! The previous step might invalidate loggerA,
      ! as the underlying vector resizes(). Check that
      ! it does not

      loggerA => manager%getLogger('A')
      loggerB => manager%getLogger('B')

      @assertEqual('A', loggerA%getName())
      @assertEqual('B', loggerB%getName())

   end subroutine test_getLogger

@test
   subroutine test_getParentPrefix()
      type (LoggerManager) :: manager

      @assertEqual('A', manager%getParentPrefix('A.B'))
      @assertEqual('A.B', manager%getParentPrefix('A.B.C'))
      @assertEqual('A.B.C', manager%getParentPrefix('A.B.C.other'))

   end subroutine test_getParentPrefix

   
@test
   subroutine test_parentage()
      type (LoggerManager) :: manager
      class (Logger), pointer :: loggerA
      class (Logger), pointer :: loggerB
      class (AbstractLogger), pointer :: parent

      manager = LoggerManager(RootLogger(WARNING))
      loggerA => manager%getLogger('A')
      loggerB => manager%getLogger('A.B')
      parent => loggerB%getParent()
      
      select type (parent)
      class is (Logger)
         @assertEqual('A', parent%getName())
      end select

   end subroutine test_parentage


@test
   subroutine test_fixup()
      type (LoggerManager) :: manager
      class (Logger), pointer :: loggerAB
      class (Logger), pointer :: parent

      manager = LoggerManager(RootLogger(WARNING))
      ! The next logger is created with a placeholder for its parent.
      loggerAB => manager%getLogger('A.B')
      parent => loggerAB%getParent()

      @assertEqual('ROOT_LOGGER', parent%getName())

   end subroutine test_fixup


@test
   subroutine test_fixup2()
      type (LoggerManager), target :: manager
      class (Logger), pointer :: loggerABC
      class (Logger), pointer :: parent

      manager = LoggerManager(RootLogger(WARNING))
      ! The next logger is created with a placeholder for its parent.
      loggerABC => manager%getLogger('A.B.C')
      parent => loggerABC%getParent()

      @assertEqual('ROOT_LOGGER', parent%getName())

   end subroutine test_fixup2
   

@test
   subroutine test_fixup3()
      type (LoggerManager), target :: manager
      class (Logger), pointer :: loggerA, loggerAB, loggerABC, loggerABD, loggerAC
      class (Logger), pointer :: parent

      manager = LoggerManager(RootLogger(WARNING))
      ! The next logger is created with a placeholder for its parent and grandparent.
      loggerABC => manager%getLogger('A.B.C')
      loggerABD => manager%getLogger('A.B.D')
      ! And another orphan to the side ...
      loggerAC => manager%getLogger('A.C')

      ! The next step should replace the placeholder at 'A.B'
      loggerAB => manager%getLogger('A.B')
      ! Check that everyone has the right parents.
!!$      parent => loggerAB%getParent()
!!$      @assertEqual('ROOT_LOGGER', parent%getName())
!!$      parent => loggerABC%getParent()
!!$      @assertEqual('A.B', parent%getName())
!!$      parent => loggerABD%getParent()
!!$      @assertEqual('A.B', parent%getName())
!!$
!!$
!!$      ! Now replace the placeholder at 'A'
!!$      loggerA => manager%getLogger('A')
!!$      ! Check that everyone has the right parents.
!!$      parent => loggerA%getParent()
!!$      @assertEqual('ROOT_LOGGER', parent%getName())
!!$      parent => loggerAC%getParent()
!!$      @assertEqual('A', parent%getName())
!!$      parent => loggerAB%getParent()
!!$      @assertEqual('A', parent%getName())
!!$
!!$      parent => loggerABC%getParent()
!!$      @assertEqual('A.B', parent%getName())
!!$      parent => loggerABD%getParent()
!!$      @assertEqual('A.B', parent%getName())

   end subroutine test_fixup3
   
   
@test
   subroutine test_parent_is_root()
      type (LoggerManager) :: manager
      class (Logger), pointer :: loggerA
      class (AbstractLogger), pointer :: parent
      type (RootLogger) :: root_node

      root_node = RootLogger(WARNING)
      manager = LoggerManager(root_node)
      loggerA => manager%getLogger('A')

      parent => loggerA%getParent()
      @assertTrue(same_type_as(parent, root_node))
      select type (parent)
      class is (RootLogger)
         @assertEqual('ROOT_LOGGER', parent%getName())
      end select

   end subroutine test_parent_is_root

! TODO: what is this testing?
! TODO: Most of these tests should be in Test_Logger   
@test
   subroutine test_passMessageToParent()
      use MockHandler_mod
      type (LoggerManager) :: manager
      class (Logger), pointer :: loggerA
      class (Logger), pointer :: loggerB

      type (MockHandler) :: handlerA
      type (MockHandler) :: handlerB

      manager = LoggerManager(RootLogger(WARNING))
      loggerA => manager%getLogger('A')
      loggerB => manager%getLogger('A.B')
      call loggerA%addHandler(handlerA)
      call loggerB%addHandler(handlerB)

   end subroutine test_passMessageToParent

   
@test
   subroutine test_caseSensitiveName()
      type (LoggerManager) :: manager
      class (Logger), pointer :: loggerA
      class (Logger), pointer :: loggerB

      manager = LoggerManager(RootLogger(WARNING))
      loggerA => manager%getLogger('A')
      loggerB => manager%getLogger('a')
      
      ! loggerA pointer may become invalid because second getLogger() call
      ! changes the data structure.  So we get it again
      loggerA => manager%getLogger('A')

      @assertFalse(associated(loggerA, loggerB))
   end subroutine test_caseSensitiveName



   @test
   subroutine test_propagate()
      use MockHandler_mod
      use PFL_Formatter_mod
      type (LoggerManager) :: manager
      class (Logger), pointer :: parent
      class (Logger), pointer :: child

      type (MockHandler) :: h
      type (MockBuffer), target :: buffer

      manager = LoggerManager(RootLogger(ERROR))
      parent => manager%getLogger('parent')
      child => manager%getLogger('parent.child')
      
      h = MockHandler(buffer)
      call h%setFormatter(Formatter('%(message)'))

      call parent%addHandler(h)
      buffer%buffer = ''
      call child%info('test_info') ! should invoke handler through parent
      @assertEqual('test_info', buffer%buffer)

   end subroutine test_propagate
   
   @test
   subroutine test_propagate_off()
      use MockHandler_mod
      use PFL_Formatter_mod
      type (LoggerManager) :: manager
      class (Logger), pointer :: parent
      class (Logger), pointer :: child

      type (MockHandler) :: h
      type (MockBuffer), target :: buffer

      manager = LoggerManager(RootLogger(ERROR))
      parent => manager%getLogger('parent')
      child => manager%getLogger('parent.child')
      call child%setPropagate(.false.)
      
      h = MockHandler(buffer)
      call h%setFormatter(Formatter('%(message)'))

      call parent%addHandler(h)

      buffer%buffer = ''
      call child%info('test_info') ! should invoke handler through parent
      @assertEqual('', buffer%buffer)

   end subroutine test_propagate_off
   

end module Test_LoggerManager_mod
